buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.0'
	}
}

allprojects {
	repositories {
		jcenter()
	}
}

apply plugin: 'com.android.application'

dependencies {
	compile 'com.squareup.picasso:picasso:2.5.2'
}

android {
	compileSdkVersion 24
	buildToolsVersion '24'

	sourceSets.main {
		manifest.srcFile 'AndroidManifest.xml'
		java.srcDirs = ['src']
		resources.srcDirs = ['src']
		aidl.srcDirs = ['src']
		renderscript.srcDirs = ['src']
		res.srcDirs = ['res']
		assets.srcDirs = ['assets']
	}

	if (new File('keystore.properties').exists()) {
		def Properties keystoreProperties = new Properties()
		keystoreProperties.load(new FileInputStream(file('keystore.properties')))

		signingConfigs {
			general {
				storeFile file(keystoreProperties['store.file'])
				storePassword keystoreProperties['store.password']
				keyAlias keystoreProperties['key.alias']
				keyPassword keystoreProperties['key.password']
			}
		}

		buildTypes.debug.signingConfig signingConfigs.general
		buildTypes.release.signingConfig signingConfigs.general
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	lintOptions {
		abortOnError false
		disable 'ValidFragment'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}
}